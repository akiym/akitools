#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Long qw/:config posix_default no_ignore_case bundling permute/;

# from libseccomp
my $DISASM = 'scmp_bpf_disasm';
my $RESOLVER = 'scmp_sys_resolver';

my %opt = (
    arch => undef, # x86_64, x32, x86
);
GetOptions(
    'a=s' => \$opt{arch},
) or usage();

my $arch = $opt{arch};

my $bpf = shift or usage();

my %arch_def = (
    3221225534 => 'x86_64',
    1073741886 => 'x32',
    1073741827 => 'i386',
);

warn "$DISASM $bpf";
my $dump = `$DISASM $bpf`;
for my $line (split /\n/, $dump) {
    $line =~ s/\$data\[0\]/seccomp_data.nr/;
    $line =~ s/\$data\[4\]/seccomp_data.arch/;
    $line =~ s/\$data\[8\]/seccomp_data.instruction_pointer/;
    for my $i (0..5) {
        my $n = 16 + $i * 8;
        $line =~ s/\$data\[$n\]/seccomp_data.args[$i]/;
    }
    $line =~ s{(jeq )(\d+)}{
        my $val;
        if ($2 > 1024) {
            $val = $arch_def{$2};
            if (!$opt{arch}) {
                $arch = $opt{arch} = $val;
            }
        } else {
            $val = 'SYS_' . `$RESOLVER -a $arch $2`;
            chomp $val;
        }
        "$1$val";
    }e;
    print "$line\n";
}

sub usage {
    die "Usage; $0 [-a arch] bpf\n";
}
